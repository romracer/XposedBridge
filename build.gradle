buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.0'
    }
}

allprojects {
    gradle.projectsEvaluated {
       tasks.withType(JavaCompile) {
           options.compilerArgs.add('-Xbootclasspath/p:lib/HiddenAPIStubs.jar')
       }
    }
}

apply plugin: 'com.android.application'

Properties localprops = new Properties();
localprops.load(new FileInputStream(file("local.properties")))

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src', 'lib/apache-commons-lang']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            resources {
                srcDirs = ['.']
                exclude 'assets', 'src', 'lib', 'HiddenAPIStubs'
                exclude 'build', 'bin', 'gen'
                exclude '*.launch', '*.bat', '*.jardesc', '*.properties', '*.xml', '*.gradle', 'gradle*'
                exclude '*.sample', 'README.md', '.*'
            }
        }

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    defaultConfig {
        minSdkVersion 15
        maxSdkVersion 23
        targetSdkVersion 23
    }

    signingConfigs {
        release {
            storeFile file(localprops["key.store"])
            storePassword localprops["store.pass"]
            keyAlias localprops["key.alias"]
            keyPassword localprops["key.pass"]
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), \
            'proguard-project.txt'
            signingConfig signingConfigs.release
        }
    }
}
